---
title: "Quality of Match for Statistical Matches for the US"

subtitle: "American Time Use Survey 2019,  the Survey of Consumer Finances 2019, and the Annual Social and Economic Supplement 2020"

abstract-title: "Abstract"

abstract: "This paper describes the quality of the statistical matching between the March 2020 supplement to the Current Population Survey (CPS) and the 2019 American Time Use Survey (ATUS) and Survey of Consumer Finances (SCF), which are used as the basis for the 2019 Levy Institute Measure of Economic Well-Being (LIMEW) estimates for the United States. In the first part of the paper, the alignment of the datasets is examined. In the second, various aspects of the match quality are described. The results indicate that the matches are of high quality, with some indication of bias in specific cases."
---


# Data Aligment and Statistics

## ATUS {{< meta atus.year >}} -- ASEC {{< meta asec.year >}}

In order to create the synthetic dataset and transfer the time use information from the donor to the recipient dataset we need to verify that the distribution of information across surveys is comparable. To do so, we compare the distribution of individuals across the following set of variables: Gender, Parental Status, labor force Status, Presence of a Spouse, and employment status of the spouse. Since both datasets were collected within one year of each other, one should expect them to be well aligned.


```{stata}
*| echo: false
*| output: false
clear all
frame create atus_cps
frame change atus_cps

use atus_cps_19, clear

tab survey [w=nwgt],     matcell(ms0)
tab sex            survey [w=nwgt],     matcell(ms1)
tab parentyn       survey [w=nwgt],     matcell(ms2)
tab emp            survey [w=nwgt],     matcell(ms3)
tab spouse_present survey [w=nwgt],     matcell(ms4)
tab spouse_emp     survey [w=nwgt],     matcell(ms5)
matrix ms5=ms5[2..3,....]

mata: 
    tt  = st_matrix("ms0")'
	tb1 = st_matrix("ms1")\
	      st_matrix("ms2")\
		  st_matrix("ms3")\
		  st_matrix("ms4")\
		  st_matrix("ms5")
	tb1 = tb1:/tt*100
	tt  = tt,(tt[,2]:-tt[,1]):/tt[,1]*100
	tb1 = tt\(tb1,tb1[,2]:-tb1[,1])
end

mata: st_matrix("tb1",tb1)
matrix roweq   tb1 = rr1 rr2 rr2 rr3 rr3 rr4 rr4 rr5 rr5 rr6 rr6
set linesize 255
```

```{stata}
*| output: asis
*| echo: false

esttab matrix(tb1, fmt("%13.0fc 1" "%13.0fc 1" "1 1")), mlabels(, none) md ///
varlabels(r1 "N"  r2 "  Female" r3 "  Male" ///
r4 "  No" r5 "  Yes" ///
r6 "  Not Employed"  r7 "  Employed" ///
r8 "  No" r9 "  Yes" ///
r10 "  Spouse not Employed" ///
r11 "  Spouse Employed" ) ///
collab("ASEC" "ATUS" "diff") eqlabels("" "Sex" "Parental Status" "Labor Force Status" "Spouse present" "Spouse's labor force status")
display ": Summary Statistics, Alignment across Main variables " _continue
display `"{#tbl-t1}{tbl-colwidths="[35,15,15,15]"}"' 
```

```{stata}
*| echo: false
*| output: false
sum sex [w=nwgt]
local v1:display %3.1f `=r(mean)*100' 
sum parentyn [w=nwgt]
local v2:display %3.1f `=r(mean)*100' 
local v3:display %3.1f `=tb1[7,3]' 
local v4:display %3.1f `=tb1[7,1]' 
local v5:display %3.1f `=tb1[7,3]' 
```

```{stata}
*| output: asis
*| echo: false
#delimit;
display "---" _n
        "v1 : `v1' " _n
        "v2 : `v2' " _n
        "v3 : `v3' " _n
        "v4 : `v4' " _n
        "v5 : `v5' " _n
        "---";
#delimit cr
```

As can be observed in @tbl-t1, the distribution of the sample with respect to sex and parental status is almost identical for both the ASEC and ATUS, with {{< meta v1 >}} percent of the sample being male, and about {{< meta v2 >}} percent being parents. The labor force status shows a relatively larger imbalance. The ATUS indicates there is a {{< meta v3 >}} percentage point larger share of employed individuals in the sample compared to the corresponding statistic in the ASEC survey ({{< meta v4 >}} percent). The distribution of individuals across marital status presents a less severe imbalance. The statistics show that the share of married individuals is larger ({{< meta v5 >}} percentage points) in the ATUS compared to the ASEC. In terms of the spouse's labor force status, the differences in the distribution among married individuals are negligible.

```{stata}
*| echo: false
*| output: false
capture matrix drop tb2
foreach i in hhinccl age_gr race educ child18 adultnum {
	local j = `j'+1
	tab `i' survey [w=nwgt],     matcell(ms`j')
	mata:mm=st_matrix("ms`j'");mm=mm:/colsum(mm)*100;mm=mm,mm[,2]:-mm[,1]
	mata:st_matrix("ms`j'",mm)
	matrix roweq ms`j'=rr`j'
	matrix tb2 = nullmat(tb2)\ms`j'
}
matrix jj=J(30,1,0)
local cnm:rowname jj
matrix rowname tb2 = `cnm'
```


```{stata}
*| output: asis
*| echo: false
#delimit;
esttab matrix(tb2, fmt("1" "1" "1")), mlabels(, none)  
varlabels(r1 "  <$15k" r2 "  $15k-$35k" 
r3 "  $35k-$50k" r4 "  $50k-$75k" r5 "  >$75k" 
r6 "  15 to 24" r7 "  25 to 34" r8 "  35 to 44" 
r9 "  45 to 54" r10 "  55 to 64" r11 "  65 to 74" r12 "  75 and older" 
r13 "  White"  r14 "  Black" 
r15 "  Hispanic" r16 "  Other"
r17 "  Less than high school" r18 "  High school"
r19 "  Some college" r20 "  College/grad school"
r21 "  0" r22 "  1"  r23 "  2" r24 "  3" r25 "  4 or more"
r26 "  1" r27 "  2" r28 "  3" r29 "  4" r30 "  5 or more") 
collab("ASEC" "ATUS" "diff") md
eqlabels("Household income category" 
"Age category" 
"Race" 
"Educational attainment"
"Number of children under 18 in household" 
"Number of persons in household over 18");
#delimit cr
display ": Summary Statistics, Alignment across Selected variables " _continue
display `"{#tbl-t2}{tbl-colwidths="[35,15,15,15]"}"' 
```


```{stata}
*| echo: false
*| output: false
local v6:display %3.1f `=tb2[19,3]' 
local v7:display %3.1f `=tb2[13,1]' 
local v8:display %3.1f `=1' 
```

```{stata}
*| output: asis
*| echo: false
#delimit;
display "---" _n
        "v6 : `v6' " _n
        "v7 : `v7' " _n
        "v8 : `v8' " _n
        "---";
#delimit cr
```

@tbl-t2 presents statistics on additional variables that characterize the observations in both the donor and recipient datasets. The distribution across household income categories shows some imbalance, with the ATUS showing a considerably lower proportion of households in the highest income category, suggesting some undersampling of high-income households. For other demographic characteristics, such as age, race, and educational attainment, the distribution of individuals in both surveys is close. The largest observed differences across these characteristics are seen in the categories of some college ({{< meta v6 >}} percentage points) and whites ({{< meta v7 >}} percentage points), with other differences falling below {{< meta v8 >}} percentage points. Finally, in terms of household structure, the survey's distribution is close in terms of number of children in the household, with slightly larger discrepancies in terms of the number of adult persons in the household, where the ATUS indicates a smaller share of larger households.

As expected, although some differences in the distributions can be observed between both surveys, these differences are small and there are no systematic differences that might seriously affect the quality of the matching process. 


## SCF {{< meta scf.year >}} -- ASEC {{< meta asec.year >}}


Similar to the previous case, in order to create the synthetic dataset that combines the SCF and ASEC information, we use five strata variables to evaluate the aligment across surveys. These strata variables are income category, homeownership, family type, and race and age of the householder. In this case, the households/families rather than individuals are used as the unit of observation.  @tbl-t3 presents summary statistics that compare the distribution of observations within the strata variables. Since both datasets were collected within one year of each other, one should expect them to be well aligned.

```{stata}
*| echo: false
*| output: false

frame create scf_cps
frame change scf_cps

use scf_cps_19, clear

tab survey 		    [iw=wgt] , matcell(ms0)
tab hhinccl donor [iw=wgt] , matcell(ms1)
tab home_prop donor [iw=wgt] , matcell(ms2)
tab famtype donor [iw=wgt] , matcell(ms3)
tab racecl  donor [iw=wgt] , matcell(ms4)
tab agecl   donor [iw=wgt] , matcell(ms5)

mata: 
    tt  = st_matrix("ms0")'
	tb3 = st_matrix("ms1")\
	      st_matrix("ms2")\
		  st_matrix("ms3")\
		  st_matrix("ms4")\
		  st_matrix("ms5")
	tb3 = tb3:/tt*100
	tt  = tt,(tt[,2]:-tt[,1]):/tt[,1]*100
	tb3 = tt\(tb3,tb3[,2]:-tb3[,1])
end

mata: st_matrix("tb3",tb3)
matrix roweq   tb3 = rr1 rr2 rr2 rr2 rr2 rr2  rr3 rr3 rr3 rr4 rr4 rr4 rr5 rr5 rr5 rr5 rr6 rr6 rr6 rr6
set linesize 255
```

```{stata}
*| output: asis
*| echo: false

esttab matrix(tb3, fmt("%13.0fc 1" "%13.0fc 1" "1 1")), mlabels(, none) md ///
varlabels( ///
r1 "N"  r2 "  <$20K" r3 "  $20k-$50k" r4 "  $50k-$75k" ///
r5 "  $75K-$100k" r6 "  >$100k" ///
r7 "  Renter" r8 "  Owner w/Mortgage" r9 "  Owner wo/Mortgage" ///
r10 "  Couple" r11 "  Single Female" r12 "  Single Male" ///
r13 "  White" r14 "  Black" r15 "  Hispanic" r16 "  Other" ///
r17 "  <35 and younger" r18 "  35 to 49" r19 "  50 to 65" ///
r20 "  >65 and older" ) ///
collab("ASEC" "SCF" "diff") ///
eqlabels("" "Household income category" ///
"Homeownership" "Family type" ///
"Race category" "Age Category")
display ": Summary Statistics, Alignment across Main variables " _continue
display `"{#tbl-t3}{tbl-colwidths="[35,15,15,15]"}"' 
```



```{stata}
*| echo: false
*| output: false
* Look at Group specific values
local v11:display %3.1f `=tb3[10,3]' 
local v12:display %3.1f `=tb3[11,3]' 
local v13:display %3.1f `=tb3[8,3]' 
```

```{stata}
*| output: asis
*| echo: false
#delimit;
display "---" _n
        "v9 : 5.8 " _n
        "v10 : 3.0 " _n
        "v11 : `v11' " _n
        "v12 : `v12' " _n
        "v13 : `v13' " _n
        "---";
#delimit cr
```

As observed in @tbl-t3, the distribution of households across income categories shows good balance across both samples, displaying at most a {{< meta v9 >}} percentage point difference when comparing households with highest income group.  The SCF has a smaller share of middle-to-high-income households. This difference, however could be attributed to standard income growth across years. Nevertheless, for the matching, we aim to use income ranking rather than income levels to ensure good matching. Based on race and age, the distribution is very well balanced, with a less than {{< meta v10 >}}  percentage point difference in the distributions, and a small underrepresentation of Hispanic and other races in the SCF. The largest distributional differences are present across family type and homeownership. The SCF dataset shows a slightly larger share of households within the "couples" categories ({{< meta v11 >}} percentage points), while households with single females are underrepresented ({{< meta v12 >}} percentage points). Regarding homeownership, both samples present similar shares of renters and homeowners. Within the homeowners category, however, the ASEC underrepresents households with mortgages in about {{< meta v13 >}} percent of the instances compared to the SCF. To reduce the impact of the imbalance on the matching, we make use to include and indicator for "mortage" in the imputation process.

@tbl-t4 presents statistics on additional variables that characterize the observations in both datasets. Information on education and occupation categories corresponds to that of the householder. The surveys are well balanced in terms of the educational attainment of the householder, the number of persons within the household, and the occupational categories. Although some imbalance is observed regarding the share of individuals with a Highschool degree.

```{stata}
*| echo: false
*| output: false
capture matrix drop tb2
gen nump4=nump
replace nump4=4 if nump>4
capture matrix drop tb4
foreach i in educl sex_1 nump4  occ {
	local j = `j'+1
	tab `i' survey [iw=wgt],     matcell(ms`j')
	mata:mm=st_matrix("ms`j'");mm=mm:/colsum(mm)*100;mm=mm,mm[,2]:-mm[,1]
	mata:st_matrix("ms`j'",mm)
	matrix roweq ms`j'=rr`j'
	matrix tb4 = nullmat(tb4)\ms`j'
}
matrix jj=J(17,1,0)
local cnm:rowname jj
matrix rowname tb4 = `cnm'
```


```{stata}
*| output: asis
*| echo: false
#delimit;
esttab matrix(tb4, fmt("1" "1" "1")), mlabels(, none)  
varlabels(r1 "  Less than high school" r2 "  High school grad"  
r3 "  Some college" r4 "  College or higher"  
r5 "  Male" r6 "  Female"  
r7 "  1 Member" r8 "  2 Members"  r9 "  3 Members" r10 "  4 or more Member"  
r11 "  Occ1: 37–199" r12 "  Occ2: 203–389"  
r13 "  Occ3: 403–469 & 903–905"   
r14 "  Occ4: 503–699" r15 "  Occ5: 703–889"  
r16 "  Occ6: 473–499" r17 "  Other" )  
collab("ASEC" "SCF" "diff") md
eqlabels("Education category" 
"Sex of householder" 
"Number of members in household" 
"Occupation category");
#delimit cr
display ": Summary Statistics, Alignment across Selected variables " _continue
display `"{#tbl-t4}{tbl-colwidths="[35,15,15,15]"}"' 
```
